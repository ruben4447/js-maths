// Function declaration
func hi() { println("Hello"); }
hi();

// Function expression (remember semicolon at end)
hi = func() { println("Hello"); };
hi();

// Function declaration without parenthesis
func hi { println("Hello"); }
hi();

// With arguments
func add(a, b) { a + b; }
println(add(5 + i, 3));

// With argument types
func add(a: real, b: real) { a + b; }
println(add(5 + i, 3));

// With return type
func tostring(arg: any): string { arg }
x = 12;
println("x = {type(x)} {x}");
x = tostring(x);
println("x = {type(x)} {x}");

// Optional arguments
func greet(?name: string) {
  println("Hello, " + (name ?? "Unknown"));
}
greet();
greet("Joe");

// Pass by reference
func swap(x: ref any, y: ref any) {
  let tmp = x;
  x = y;
  y = tmp;
}

a = 5, b = 10;
print("a:" + a + "; b:" + b);
swap(a, b);
println(" --> a:" + a + "; b:" + b);

// Default value
func greet(name = "Default") {
  println("Hello, " + name);
}
greet();
greet("Joe");

// Default argument as an expression
let a = 4;

func f(x: string = (1 + a)) {
  println("x = ({type (x)}) {x}");
}

f();
a = 10;
f();

// Default value with type
func greet(name: string = "Default") {
  println("Hello, " + name);
}
greet();
greet(12 + i);

// Lambda function
f = x -> x * 2, pow = (a, b = 2) -> a ** b;
println(f(10));
println(pow(3));
println(pow(3, 4));

exit(1);

// '...' arguments: compact to array
func print_array(...items) {
  println("[{len(items)}] \{{join(items, ',')}}");
}
print_array(65, 66, 67, 68, 69);

// ... with a type
func print_array(...items: char) {
  println("[{len(items)}] \{{join(items, ',')}}");
}
print_array(65, 66, 67, 68, 69);

// In middle of parameter list
func multi(a, ...b, c) {
  println("{a=}; {b=}; {c=}");
}

multi(1, 2); // a=1, b=[], c=2
multi(1, 2, 3); // a=1, b=[2], c=3

// Using '...' to expand arguments
powargs = [4, 2];
println("{powargs=} : {pow(...powargs) = }");